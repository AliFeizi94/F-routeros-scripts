#!rsc
# RouterOS script: cloud-backup
# Copyright (c) 2013-2019 Christian Hesse <mail@eworm.de>
#
# upload backup to MikroTik cloud

:global Identity;
:global BackupPassword;

:global SendNotification;

# get some system information
:local BoardName [ / system resource get board-name ];
:local RouterBoard [ / system routerboard get ];
:local Update [ / system package update get ];

:do {
  # we are not interested in output, but print without count-only is
  # required to fetch information from cloud
  / system backup cloud print as-value;
  :if ([ / system backup cloud print count-only ] > 0) do={
    / system backup cloud upload-file action=create-and-upload \
        password=$BackupPassword replace=[ get ([ find ]->0) name ];
  } else={
    / system backup cloud upload-file action=create-and-upload \
        password=$BackupPassword;
  }
  :local Cloud [ / system backup cloud get ([ find ]->0) ];

  $SendNotification "Cloud backup" \
    ("Uploaded backup for " . $Identity . " to cloud.\n\n" . \
      "Board name:     " . $BoardName . "\n" . \
      "Model:          " . $RouterBoard->"model" . "\n" . \
      "Serial number:  " . $RouterBoard->"serial-number" . "\n" . \
      "Hostname:       " . $Identity . "\n" . \
      "Channel:        " . $Update->"channel" . "\n" . \
      "RouterOS:       " . $Update->"installed-version" . "\n\n" . \
      "Name:           " . $Cloud->"name" . "\n" . \
      "Size:           " . $Cloud->"size" . "\n" . \
      "Download key:   " . $Cloud->"secret-download-key") "" "true";
} on-error={
  :log error ("Failed uploading backup for " . $Identity . " to cloud.");
}
