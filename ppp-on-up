#!rsc
# RouterOS script: ppp-on-up
# Copyright (c) 2013-2019 Christian Hesse <mail@eworm.de>
#
# run scripts on ppp up

:local Interface $interface;

:if ([ :typeof $Interface ] = "nothing") do={
  :log error "This script is supposed to run from ppp on-up script hook.";
  :error "Error: See log for details.";
}

:local IntName [ / interface get $Interface name ];
:log info ("PPP interface " . $IntName . " is up.");

/ ipv6 dhcp-client release [ find where interface=$IntName !disabled ];

:local Scripts {
  "update-tunnelbroker"
}

:foreach Script in=$Scripts do={
  :if ([ / system script print count-only where name=$Script ] > 0) do={
    :log debug ("Running script from ppp-on-up: " . $Script);
    / system script run $Script;
  }
}
