#!rsc
# RouterOS script: mode-button-scheduler
# Copyright (c) 2018-2020 Christian Hesse <mail@eworm.de>
# https://git.eworm.de/cgit/routeros-scripts/about/COPYING.md
#
# act on multiple mode-button presses from scheduler
# https://git.eworm.de/cgit/routeros-scripts/about/doc/mode-button.md

:global ModeButton;

:global LogPrintExit;

:local Count ($ModeButton->"count");
:local Code ($ModeButton->[ :tostr $Count ]);

:set ($ModeButton->"count") 0;
/ system scheduler remove mode-button-scheduler;

:if ([ :len $Code ] > 0) do={
  $LogPrintExit info ("Acting on " . $Count . " mode-button presses: " . $Code) false;

  :if ([ / system routerboard settings get silent-boot ] = false) do={
    :for I from=1 to=$Count do={
      :beep length=200ms;
      :delay 200ms;
    }
  } else={
    :delay 1s;
  }

  [ :parse $Code ];
} else={
  $LogPrintExit info ("No action defined for " . $Count . " mode-button presses.") false;
}
