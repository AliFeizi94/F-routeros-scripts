#!rsc
# RouterOS script: netwatch-notify
# Copyright (c) 2020 Christian Hesse <mail@eworm.de>
# https://git.eworm.de/cgit/routeros-scripts/about/COPYING.md
#
# monitor netwatch and send notifications
# https://git.eworm.de/cgit/routeros-scripts/about/doc/netwatch-notify.md

:global NetwatchNotify;

:global ParseKeyValueStore;
:global LogPrintExit;
:global SendNotification;

:if ([ :typeof $NetwatchNotify ] = "nothing") do={
  :set NetwatchNotify [ :toarray "" ];
}

:foreach Host in=[ / tool netwatch find where comment~"^notify," ] do={
  :local HostVal [ / tool netwatch get $Host ];
  :local HostName ([ $ParseKeyValueStore ($HostVal->"comment") ]->"hostname");

  :local Metric { "count"=0; "notified"=false };
  :if ([ :typeof ($NetwatchNotify->$HostName) ] = "array") do={
    :set $Metric ($NetwatchNotify->$HostName);
  }

  :if ($HostVal->"status" = "up") do={
    $LogPrintExit debug ("Host " . $HostName . " (" . $HostVal->"host" . ") is up.") false;
    :local Count ($Metric->"count");
    :set ($Metric->"count") 0;
    :if ($Metric->"notified" = true) do={
      $SendNotification ("Netwatch Notify: \E2\9C\85 " . $HostName . " up") \
        ("Host " . $HostName . " (" . $HostVal->"host" . ") is up since " . $HostVal->"since" . ".\n" . \
        "It was down for " . $Count . " checks.");
    }
    :set ($Metric->"notified") false;
  } else={
    :set ($Metric->"count") ($Metric->"count" + 1);
    $LogPrintExit info ("Host " . $HostName . " (" . $HostVal->"host" . ") is down for " . \
      $Metric->"count" . " checks.") false;
    :if ($Metric->"count" >= 5 && $Metric->"notified" != true) do={
      $SendNotification ("Netwatch Notify: \E2\9D\8C " . $HostName . " down") \
        ("Host " . $HostName . " (" . $HostVal->"host" . ") is down since " . $HostVal->"since" . ".");
      :set ($Metric->"notified") true;
    }
  }
  :set ($NetwatchNotify->$HostName) { "count"=($Metric->"count"); "notified"=($Metric->"notified") };
}
