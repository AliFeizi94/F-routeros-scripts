#!rsc
# RouterOS script: update-tunnelbroker
# Copyright (c) 2013-2019 Christian Hesse <mail@eworm.de>
#                         Michael Gisbers <michael@gisbers.de>

:global CertificateAvailable;

:if ([ / ip cloud get ddns-enabled ] != true) do={
  :log error "IP cloud DDNS is not enabled.";
  :error "Error: See log for details.";
}

# Get the current ip address from cloud
/ ip cloud force-update;
:while ([ / ip cloud get status ] != "updated") do={
  :delay 1s;
}
:local PublicAddress [ / ip cloud get public-address ];

:foreach Interface in=[ / interface 6to4 find where comment~"^tunnelbroker" !disabled ] do={
  :local IntName [ / interface 6to4 get $Interface name ];
  :local LastAddress [ / interface 6to4 get $Interface local-address ];

  :if ($PublicAddress != $LastAddress) do={
    :local Comment [ :toarray [ / interface 6to4 get $Interface comment ] ];
    :local User [ :pick ($Comment->1) 5 99 ];
    :local Pass [ :pick ($Comment->2) 5 99 ];
    :local Id [ :pick ($Comment->3) 3 99 ];

    $CertificateAvailable "Starfield Secure Certificate Authority - G2" "starfield";
    :log info ("Local address changed, sending UPDATE to tunnelbroker! New address: " . $PublicAddress);
    / tool fetch check-certificate=yes-without-crl \
        ("https://ipv4.tunnelbroker.net/nic/update\?hostname=" . $Id) \
        user=$User password=$Pass keep-result=no;
    / interface 6to4 set $Interface local-address=$PublicAddress;
  } else={
    :log debug ("All tunnelbroker configuration is up to date for interface " . $IntName . ".");
  }
}
