#!rsc
# RouterOS script: dhcp-to-dns
# Copyright (c) 2013-2019 Christian Hesse <mail@eworm.de>
#
# check DHCP leases and add/remove/update DNS entries

:global CharacterReplace;

:global Identity;
:global Domain;
:global HostNameInZone;

:local Zone;
:if ($HostNameInZone = true) do={
  :set Zone ("dhcp." . $Identity . "." . $Domain);
} else={
  :set Zone ("dhcp." . $Domain);
}
:local Ttl 5m;
:local CommentPrefix "managed by dhcp-to-dns for ";

:foreach Static in=[ / ip dns static find where comment ~ $CommentPrefix ] do={
  :local MacAddress [ $CharacterReplace [ / ip dns static get $Static comment ] $CommentPrefix "" ];
  :local IpAddress [ / ip dns static get $Static address ];
  :local HostName [ / ip dns static get $Static name ];
  :if ([ / ip dhcp-server lease print count-only where mac-address=$MacAddress address=$IpAddress dynamic=yes ] > 0) do={
    :log debug ("Lease for " . $MacAddress . " (" . $HostName . ") still exists. Not deleting DNS entry.");
  } else={
    :local Found false;
    :log info ("Lease expired for " . $MacAddress . " (" . $HostName . "), deleting DNS entry.");
    / ip dns static remove $Static;
  }
}

:foreach Lease in=[ / ip dhcp-server lease find where dynamic=yes ] do={
  :local Mac [ / ip dhcp-server lease get $Lease mac-address ];
  :local DhcpIp [ / ip dhcp-server lease get $Lease address ];
  :local Comment ($CommentPrefix . $Mac);
  :local HostName [ $CharacterReplace [ / ip dhcp-server lease get $Lease host-name ] " " "" ];
  :if ($HostName = "") do={
    :set HostName [ $CharacterReplace [ / ip dhcp-server lease get $Lease mac-address ] ":" "-" ];
  }

  :local Fqdn ($HostName . "." . $Zone);
  :local DnsNode [ / ip dns static find where name=$Fqdn ];
  :if ([ :len $DnsNode ] > 0) do={
    :local DnsIp [ / ip dns static get $DnsNode address ];
    :local Leases [ / ip dhcp-server lease find where host-name=$HostName dynamic=yes ];
    :local HostNameCount [ / ip dhcp-server lease print count-only where host-name=$HostName dynamic=yes ];
    :if ($HostNameCount > 1) do={
      :foreach J,Lease in=$Leases do={
        :if ($J + 1 = $HostNameCount) do={
          :set DhcpIp [ / ip dhcp-server lease get $Lease address ];
        }
      }
    }

    :if ($DnsIp = $DhcpIp) do={
      :log debug ("DNS entry for " . $Fqdn . " does not need updating.");
    } else={
      :log info ("Replacing DNS entry for " . $Fqdn . ", new address is " . $DhcpIp . ".");
      / ip dns static set name=$Fqdn address=$DhcpIp ttl=$Ttl comment=$Comment $DnsNode;
    }
  } else={
    :log info ("Adding new DNS entry for " . $Fqdn . ", address is " . $DhcpIp . ".");
    / ip dns static add name=$Fqdn address=$DhcpIp ttl=$Ttl comment=$Comment;
  }
}
